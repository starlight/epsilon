(define-library
  (epsilon transform)
  (import
    scheme
    r7rs
    srfi-2)
  (export
    %fail
    %identity
    %unit
    %bind
    %nothing
    *alternative
    *sequence
    *maybe
    *star
    *plus
    *is
    *not
    *end)

  (begin
    ;  -> ğ’™ -> âŠ¥
    ;  => âŠ¥
    (define ((%fail) input)
      #f)

    ; ğ‘§ -> ğ’™ -> ğ‘§.ğ’™
    ; ğ‘§ => ğ‘§êœ›
    (define ((%unit value) input)
      (cons value input))

    ;  -> ğ’™ -> âˆ….ğ’™
    ;  => âˆ…êœ›
    (define (%nothing)
      (%unit '()))

    ;  -> ğ’™ -> ğ’™ âˆ¨ âŠ¥
    ;  => ğ‘¥êœ› âˆ¨ âŠ¥
    (define ((%identity) input)
      (if (null? input) #f input))

    ; ğ‘ ğ‘' => ğ‘§áµ‡ | ğ‘'ğ‘§áµƒ âˆ¨ âŠ¥
    (define ((%bind parser combinator) input)
      (and-let*
        ((result (parser input)))
        ((combinator (car result)) (cdr result))))

    ; ğ‘ => Â¬ğ‘§áµƒ âˆ´ âˆ…êœ› âˆ¨ âŠ¥
    (define ((*not parser) input)
      (and
        (not (parser input))
        ((%nothing) input)))

    ; ğ‘ ğ‘.. => ğ‘§áµƒ âˆ¨ ğ‘§áµ‡.. âˆ¨ âŠ¥
    (define ((*alternative parser . parsers) input)
      (or
        (parser input)
        (and
          (not (null? parsers))
          ((apply *alternative parsers) input))))

    ; ğ‘ ğ‘.. => ğ‘§áµƒ âˆ§ ğ‘§áµ‡.. âˆ¨ âŠ¥
    (define (*sequence parser . parsers)
      (%bind
        parser
        (lambda (first)
          (%bind
            (if (null? parsers) (%nothing)
              (apply *sequence parsers))
            (lambda (rest)
              (%unit (cons first rest)))))))

    ; ğ‘“ ğ‘¦.. => ğ‘“ğ‘¥ğ‘¦.. âˆ´ ğ‘¥êœ› âˆ¨ âŠ¥
    (define (*is predicate . args)
      (%bind
        (%identity)
        (lambda (value)
          (if (apply predicate value args)
            (%unit value)
            (%fail)))))

    ; ğ‘ => ğ‘§áµƒ âˆ¨ âˆ…êœ›
    (define (*maybe parser)
      (*alternative parser (%nothing)))

    ; ğ‘ => ğ‘§áµƒ+ âˆ¨ âˆ…êœ›
    (define (*star parser)
      (*maybe (*plus parser)))

    ; ğ‘ => ğ‘§áµƒ âˆ§ ğ‘§áµƒ* âˆ¨ âŠ¥
    (define (*plus parser)
      (*sequence parser (*star parser)))

    ;  => Â¬ğ‘¥ âˆ´ âˆ…êœ› âˆ¨ âŠ¥
    (define (*end)
      (*not (%identity)))))
